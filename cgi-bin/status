#!@PERL@

use strict;
use warnings;

use lib '@CGIBINDIR@';
# use admctrl;
use civs_common;

use CGI qw(:standard);
use File::stat;
use DB_File;
undef $/;

&HTML_Header("CIVS Server Status");
&CIVS_Header("Server Status");

my @rows = ();
push @rows, th(['Title', 'Supervisor', 'Public/Private', '# Authorized',
                '# Votes', 'Started?', 'Stopped?', 'Uses auth. key?',
                'Last updated']);

my $elections_dir = $home."/elections/";
opendir(DIR, $elections_dir) or die "Can't open directory $elections_dir: $!";
while (defined(my $file = readdir(DIR))) {
    next if !defined($file);
    next if $file =~/^\.\.?$/;    # skip . and ..
    next unless -d "$elections_dir/$file";   # skip non-directories
    next unless $file =~ m/^E_[0123456789abcdef]+/;   # skip non-elections
    
    my $election_dir = "$elections_dir/$file";
    my $election_data = $election_dir."/election_data";
    my $vote_data = $election_dir."/vote_data";
    my $started_file = $election_dir."/started";
	my $stopped_file = $election_dir."/stopped";

    my (%edata, %vdata);
    tie %edata, "DB_File", $election_data, O_RDONLY, 0777, $DB_HASH;
    tie %vdata, "DB_File", $vote_data, O_RDONLY, 0666, $DB_HASH;

    my $name = $edata{'name'};
    if (!defined($name)) { $name = '(undefined)'; }
    my $title = $edata{'title'};
    my $email_addr = $edata{'email_addr'};
    if (!defined($email_addr)) {
	$email_addr = "(no addr)";
    }
	my $public = (defined($edata{'public'}) && $edata{'public'} eq 'yes') ? "Public" : "Private";
    my $num_auth = $edata{'num_auth'};
    if (!defined($num_auth)) { $num_auth = '&ndash;'; }
    my $num_votes = $vdata{'num_votes'};
    if (!defined($num_votes)) { $num_votes = '&ndash;'; }
    my $started = (-e $started_file) ? "Yes" : "No";
    my $stopped = (-e $stopped_file) ? "Yes" : "No";
    my $auth_key = (defined($edata{'hash_authorization_key'})) ? "Yes" : "No";
    my $st = stat($election_dir."/vote_log");
    my $last_update = 'N/A';
    $last_update = localtime($st->mtime) if defined($st);
    if (defined($title)) {
	$title = "\"$title\"";
    } else {
	$title = '(no title)';
    }

    push @rows, Tr(td([a({-href=>'@CIVSBINURL@/results@PERLEXT@?id='.$file}, small($title)),
    #push @rows, Tr(td([$file,
                       "$name (" . a({-href=>"mailto:$email_addr"},$email_addr) . ")",
                       $public,
                       $num_auth,
                       $num_votes,
                       $started,
                       $stopped,
                       $auth_key,
                       small($last_update)
                       ]));

    untie %edata;
    untie %vdata;
}

print table({}, @rows);
            
print end_html();


